!$Revision:$
!$Date:$
!$RCSfile:$
!
!     ***********************************
!     * Authors and copyright:           *
!     * Alexandre Bonvin, Utrecht        *
!     * No warranty implied or expressed *
!     * All rights reserved              *
!     ************************************

!move molecules far apart
@separate.cns(Data=$Data; Toppar=$Toppar)

igroup
  interaction (not (resname ANI or resname DAN)) (not (resname ANI or resname DAN))
  interaction (    resname ANI) (    resname ANI)
  interaction (    resname DAN) (    resname DAN)
end

evaluate ($eintfree = 0.0)
minimize powell nstep=50 drop=10.0 nprint=25 end
param nbonds eps=1.0 end end
energy end
evaluate ($eintfree = $bond + $angl + $impr + $dihe + $vdw + $elec)

display FREE MOLECULES INTERNAL ENERGY = $eintfree

{====>} {* define ASA-based solvation parameters *}
inline @def_solv_param.cns

{====>} {* buried surface area *}
evaluate ($esolfree = 0)
evaluate ($nchain1 = 1)
evaluate ($saafree = 0)
while ($nchain1 <= $data.ncomponents) loop nloop1
  coor orient sele=(segid $Toppar.prot_segid_$nchain1 and not (resn TIP3 or resn DMS)) end
  {====>} {* buried surface area *}
  do (rmsd = 0) (all)
  do (store2 = 0) (all)
  surface mode=access accu=0.075 rh2o=1.4 sele=(segid $Toppar.prot_segid_$nchain1 and not (resn TIP3 or resn DMS)) end
  show sum (rmsd) (segid $Toppar.prot_segid_$nchain1 and not (resn TIP3 or resn DMS))
  evaluate ($saafree = $saafree + $result)
  do (store2 = rmsd * store1) (segid $Toppar.prot_segid_$nchain1 and not (resn TIP3 or resn DMS))
  show sum (store2) (segid $Toppar.prot_segid_$nchain1 and not (resn TIP3 or resn DMS))
  evaluate ($esolfree = $esolfree + $result)
  evaluate ($nchain1 = $nchain1 + 1)
end loop nloop1
     
